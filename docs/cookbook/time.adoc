== Times & dates

Tick is flexible with the way in which times and dates are created; ergo,
increasing efficiency.
Times and dates can be easily stripped down to smaller modules of time,
likewise they can be built up into complete instants.

CAUTION: Extracting the date from an Instant (or other fields, such as time) you will 
         get a local result.

For example: 

[source.code,clojure]
----
(t/date (t/instant "1999-12-31T00:59:59Z"))
----

The result maybe be `#time/date"1999-12-31"` or not, depending on your browser timezone 
(or timezone of the jvm). 

To get the date (or other fields) from an Instant in UTC, we must first go via a UTC ZonedDateTime"

[source.code,clojure]
----
(->
   (t/instant "1999-12-31T00:59:59Z")
   (cljc.java-time.zoned-date-time/of-instant (t/zone "UTC"))
   (t/date))
----

=== Create time

====
A specific time can be produced in multiple ways with varying degrees of precision:
[source.code,clojure]
----
(t/time "12:34")
----

[source.code,clojure]
----
(t/time "12:34:56.789")
----

[source.code,clojure]
----
(t/new-time 12 34)
----

[source.code,clojure]
----
(t/new-time 12 34 56 789000000)
----
====

=== Get the time

====
To get the current time:

[source.code,clojure]
----
(t/time)
----

[source.code,clojure]
----
(t/new-time)
----
====

====
Or the time from an instant:

[source.code,clojure]
----
(t/time (t/instant "1999-12-31T00:59:59Z"))
----
====

====
Get the current time in another time-zone:

[source.code,clojure]
----
(t/time (t/in (t/now) "Australia/Darwin"))
----
====

====
Get a specific unit of time:
[source.code,clojure]
----
(t/hour (t/instant "1999-12-31T23:59:59Z"))
----
[source.code,clojure]
----
(t/minute (t/instant "1999-12-31T23:59:59Z"))
----
[source.code,clojure]
----
(t/second (t/instant "1999-12-31T23:59:59Z"))
----
====

=== Create a date
====
Creating dates is done in much the same way as creating time.
[source.code,clojure]
----
(t/date "2000-01-01")
----
[source.code,clojure]
----
(t/new-date 2000 01 01)
----
====

=== Get the date
====
To get the current date:

[source.code,clojure]
----
(t/date)
----
[source.code,clojure]
----
(t/new-date)
----
====

====
Or the date from an instant:
[source.code,clojure]
----
(t/date (t/instant "1999-12-31T23:59:59Z"))
----
====

====
Get the date in another time-zone:
[source.code,clojure]
----
(t/date (t/in (t/instant "1999-12-31T23:59:59Z") "Australia/Darwin"))
----
====

====
Get a specific part of the date:
[source.code,clojure]
----
(t/year (t/instant "1999-12-31T23:59:59Z"))
----
[source.code,clojure]
----
(t/month (t/instant "1999-12-31T23:59:59Z"))
----
[source.code,clojure]
----
(t/day-of-month (t/instant "1999-12-31T23:59:59Z"))
----
====


=== Build up times and dates
A unique feature of tick is that you can treat individual units of time
as modular, making it easy to build up and break down time into components.

====
Break up an instant:

----
(defn instant-breakdown
  "Takes an instant of time and breaks it down into units."
  [t]
  {:day  (t/day-of-week t)
   :month  (t/month t)
   :dd (t/day-of-month t)
   :MM (t/int (t/month t))
   :yyyy (t/int (t/year t))
   :mm (t/minute t)
   :HH (t/hour t)
   :ss (t/second t)})
----

====

We can treat the individual units of time as building blocks:

.Tick Time Blocks
[options="header",valign="center"]
|====
5+|Time 3+|Date |Zone

5+|(t/time) 3+|(t/date)	|(t/zone)

|(t/hour)|(t/minute) 3+|(t/second)|(t/year)|(t/month)|(t/day-of-month)|-

|- |-|(t/millisecond)|(t/microsecond)|(t/nanosecond)|- |- |- |-
|====

====
Make up a `time`

If we want it to be half-past the current hour:
[source.code,clojure]
----
(t/new-time (t/hour (t/instant)) 30)
----
Or about lunch time:
[source.code,clojure]
----
(t/new-time 13 (t/minute (t/instant)))
----
====

====
Make up a `date-time`
[source.code,clojure]
----
(t/at (t/date "2018-01-01") (t/time "13:00"))
----
[source.code,clojure]
----
(t/on (t/time "13:00") (t/date "2018-01-01"))
----
[source.code,clojure]
----
(-> (t/tomorrow)
    (t/at (t/midnight)))
----
[source.code,clojure]
----
(-> (t/noon)
    (t/on (t/yesterday)))
----


Make up a `Zoned-Date-Time`
[source.code,clojure]
----
(-> (t/tomorrow)
    (t/at (t/midnight))
    (t/in "Europe/Paris"))
----
[source.code,clojure]
----
(-> (t/tomorrow)
    (t/at (t/midnight))
    (t/in (t/zone)))
----
====


=== Time and Date manipulation
====
Give a date a set time in the future:

[source.code,clojure]
----
(t/>> (t/date "2000-01-01") (t/new-period 1 :months))
----

[source.code,clojure]
----
(t/>> (t/date "2000-01-01") (t/new-period 4 :weeks))
----

[source.code,clojure]
----
(t/>> (t/date "2000-01-01") (t/new-period 30 :days))
----

[source.code,clojure]
----
(t/>> (t/date "2000-01-01") (t/+ (t/new-period 5 :days)
                                (t/new-period 1 :weeks)
                                (t/new-period 10 :months)))
----

Or past:

[source.code,clojure]
----
(t/<< (t/date "2000-01-01") (t/new-period 1 :years))
----
====

====
Move around in time:
[source.code,clojure]
----
(t/>> (t/time "12:00") (t/new-duration 5 :minutes))
----

[source.code,clojure]
----
(t/<< (t/time "12:00") (t/new-duration 5 :hours))
----

[source.code,clojure]
----
(t/>> (t/time "12:00") (t/+ (t/new-duration 5 :seconds)
                           (t/new-duration 5 :millis)
                           (t/new-duration 5 :micros)
                           (t/new-duration 5 :nanos)))
----

Increasing a time by a duration of day magnitude will leave the time
alone - `12:00` in 5 days is still `12:00` (ignoring daylight savings)

[source.code,clojure]
----
(t/>> (t/time "12:00") (t/new-duration 5 :days))
----
====

====
Truncate time to a desired precision:

[source.code,clojure]
----
(t/truncate (t/time "10:30:59.99") :minutes)
----
====

====
Give the am pm time:
----
(defn twelve-hour-time
  "Takes a time and gives the 12 hour display"
  [t]
  (let [minute (t/minute t)
        hour (t/hour t)]
    (cond
      (= (t/noon) t)
      "12:00 NOON"

      (>= hour 13)
      (format "%02d:%02d PM" (- hour 12) minute)

      (>= hour 12)
      (format "%02d:%02d PM" hour minute)

      (< hour 12)
      (format "%02d:%02d AM" hour minute))))

----
NOTE: "12 noon is by definition neither *ante meridiem* (before noon) nor *post
meridiem* (after noon), then 12 a.m. refers to midnight at the start of the
specified day (00:00) and 12 p.m. to midnight at the end of that day (24:00)"
- http://www.npl.co.uk/reference/faqs/is-midnight-12-am-or-12-pm-faq-time[NPL]
====
